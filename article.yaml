~version: "2.1"

## dont generate instant view for music player articles
?path:     /.+
!path_not: /expo/music/.+

## TODO: join channel button
channel: "@discoursio"
## body - article content with main-content class
body:     //div[has-class("main-content")]
## site name
site_name: Discours

# fix nesting
$problems: $body
@while( $problems ) {
  $test: $body//*[self::*[has-class("fb-post")] or self::blockquote or self::aside or self::figure or self::img[not(./ancestor::figure)] or self::iframe[not(./ancestor::figure)] or self::script[@data-telegram-post] or self::slideshow[not(./ancestor::figure)] or self::video[not(./ancestor::figure)] or self::audio[not(./ancestor::figure)] or self::related or self::footer or self::table[not(ancestor::table) and not(descendant::table)]]
  $problems: $test/self::*[ancestor::anchor or ancestor::blockquote[not(contains(@class,"instagram"))] or ancestor::aside or ancestor::footer or ancestor::strong or ancestor::b or ancestor::em or ancestor::i or ancestor::ins or ancestor::u or ancestor::del or ancestor::s or ancestor::strike or ancestor::code or ancestor::kbd or ancestor::samp or ancestor::tt or ancestor::mark or ancestor::sup or ancestor::pic or ancestor::sub or ancestor::a or ancestor::reference or ancestor::h1 or ancestor::h2 or ancestor::h3 or ancestor::h4 or ancestor::h5 or ancestor::h6 or ancestor::p or ancestor::pre or ancestor::hr or ancestor::img or ancestor::br]
  @split_parent: $problems
}




#fix empty paras
@remove: //p[not(text()) and not(node())]

# fundamentals
# title, category, author, date
title: $body//h1[1]
kicker: //span[has-class("category")]
author:   $body//span[has-class("author")]
author_url: //span[has-class("author")]/a/@href
published_date: //div[has-class("date-time")]

# set image captions for standalone images
@wrap(<figure>): //img[not(./ancestor::*[has-class("carousel-container")])]
<figcaption>: //p[has-class("image-description")]
@append_to("./prev-sibling::figure"): //figcaption


# post image subscript
@wrap(<figure>, class, "title-image"): //img[has-class("expo-item-title-image")]
<figcaption>: (//p[has-class("image-description")])[1]
@append_to(//figure[has-class("title-image")])


# lead
@wrap_inner(<b>): //*[has-class("lead")]
@wrap(<lead>): //a[.//*[has-class("lead")]]
<lead>: //b/span[./span[has-class("lead")]]
@combine(<lead>)

# parse carousel

<div>: //div[has-class("carousel-container")]/ul
<figure>: //div[has-class("carousel-container")]/*/li


<slideshow>: //div[has-class("carousel-container")]

# if there is caption under pictures
@if (//p[@id="expo-modal-label"]) {
  @after(<figcaption>, text, @title): //slideshow/*/*/img
  @append(@text): //slideshow/*/*/figcaption
  ## regex capture cleaning
  @replace(" — discours.io", ""): //slideshow/*/*/figcaption/text()
#   @replace("^(.+?), ", ""): //slideshow/*/*/figcaption/text()
}

# clean carousel
@remove: //div/*[has-class("carousel-thumbs")]
@remove: //p[@id="expo-modal-label"]
@remove: //div[has-class("rn-carousel-controls")]


# pull video from list
@split_parent: //div[has-class("video-wrapper")]
$video_containers: //ul[div[has-class("video-wrapper")]]
@remove: $video_containers/li
<div>: $video_containers

## change afterpost image preview
#<pic>: //img[has-class("article__image")]
#@set_attr(src, @srcset)
#@set_attr(width, "200px")

##
@remove: //div[has-class("article__inner")]/a[position() >= 1]
@remove: //div[has-class("article__details")]/*/*[has-class("article__category") or has-class("article__author")]

## related
<related>: //div[has-class("after-post") or @id="popular-posts" or @id="popularPosts"]
# limit number of related to 5
@append_to("./ancestor::related"): //related//a
# @remove: //related/*[not(self::a)]
@remove: (//related)[2]/a[@href=(//related)[1]/a/@href]
@remove: //related/a[position() > 3]

@append_to($body): //related

@set_attr("href", "https://t.me/iv?url=https://discours.io", @href, "&rhash=9ebd7b52652e1b"): //related//a


# remove what we don't need in Instant View

## remove afterpost
#@remove: //div[has-class("after-post")]

## remove popular
#@remove: //div[@id="popular-posts"]

## remove side-banners
@remove: //side-banners

## remove left numbers
@remove: //span[has-class("lit-pager")]
@remove: //div[has-class("hide")]

# footer trash cleaned
@remove: //span[has-class("author")]

@remove: //button[has-class("slick-prev")]
@remove: //button[has-class("slick-next")]

@remove: //div[has-class("comments-wrapper")]
@remove: //new-comment
@remove: //*[@name="comments"]

# leave only tags in footer

$footer: //div[has-class("footer-post")]

@remove: $footer//div[has-class("author-time-counters")]
@remove: $footer//donate-form
@remove: $footer//div[has-class("social-buttons-absolute")]

$tags: //ul[has-class("tags")]
@remove: $tags/li/*[has-class("tag-counter")]
<div>: $tags/li
@combine(" • ")
@prepend(<b>): $tags/div
@append("Темы: "): $tags/div/b
<p>: $tags

# footer before related
@before_el(//related): $footer