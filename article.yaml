~version: "2.1"

# fix markup
<div>: //a[.//img]
<div>: //p[.//img]
<div>: //p[.//iframe]

# fundamentals

## dont generate instant view for music player articles
?path:     /.+
!path_not: /expo/music/.+

## TODO: join channel button
channel: "@discoursio"
## body - article content with main-content class
body:     //div[has-class("main-content")]
## site name
site_name: Discours

# title, category, author, date
$title: $body//h1[1]/text()
title: $title
kicker: //span[has-class("category")]
author:   $body//span[has-class("author")]
author_url: //span[has-class("author")]/a/@href
published_date: //div[has-class("date-time")]

# post image subscript
<sub>: //p[has-class("image-description")]

# lead
<b>: //p[span[has-class("lead")]]
@wrap(<p>)

# parse carousel

<div>: //div[has-class("carousel-container")]/ul
<div>: //div[has-class("carousel-container")]/ul/li

<slideshow>: //div[has-class("carousel-container")]
@after(<figcaption>, content, @title): //slideshow/*/*/img


@remove: //div/*[has-class("carousel-thumbs")]
@remove: //p[@id="expo-modal-label"]
@remove: //div[has-class("rn-carousel-controls")]

# After posts

## only 5 articles
@remove: //div[has-class("slick-track")]/div[position() >= 6]

## change afterpost image preview
#<pic>: //img[has-class("article__image")]
#@set_attr(src, @srcset)
#@set_attr(width, "200px")

##
@remove: //div[has-class("article__inner")]/a[position() >= 1]
@remove: //div[has-class("article__details")]/*/*[has-class("article__category") or has-class("article__author")]

## convert to related
<related>: //div[has-class("after-post")]

# remove what we don't need in Instant View

## remove popular
@remove: //div[@id="popular-posts"]

## remove left numbers
@remove: //span[has-class("lit-pager")]

@remove: //div[has-class("hide")]
# share buttons of social networks
@remove: //div[has-class("social-buttons-absolute")]

# footer trash cleaned
@remove: //span[has-class("author")]
@remove: //div[has-class("footer-post")]

@remove: //button[has-class("slick-prev")]
@remove: //button[has-class("slick-next")]

@remove: //div[has-class("comments-wrapper")]
@remove: //new-comment
@remove: //*[@name="comments"]


