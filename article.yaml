~version: "2.1"

# fix markup
<div>: //a[.//img]
<div>: //p[.//img]
<div>: //p[.//iframe]

# fundamentals

## dont generate instant view for music player articles
?path:     /.+
!path_not: /expo/music/.+

## TODO: join channel button
channel: "@discoursio"
## body - article content with main-content class
body:     //div[has-class("main-content")]
## site name
site_name: Discours

# title, category, author, date
title: $body//h1[1]/text()
kicker: //span[has-class("category")]
author:   $body//span[has-class("author")]
author_url: //span[has-class("author")]/a/@href
published_date: //div[has-class("date-time")]


# post image subscript
<sub>: //p[has-class("image-description")]

# lead
<b>: //p//*[has-class("lead")]
@wrap(<p>)

# parse carousel

<div>: //div[has-class("carousel-container")]/ul
<figure>: //div[has-class("carousel-container")]/*/li

<slideshow>: //div[has-class("carousel-container")]

# if there is caption under pictures
@if (//p[@id="expo-modal-label"]) {
  @after(<figcaption>, text, @title): //slideshow/*/*/img
  @append(@text): //slideshow/*/*/figcaption
  ## regex capture cleaning
  @replace(" — discours.io", ""): //slideshow/*/*/figcaption/text()
  @replace("^(.+?), ", ""): //slideshow/*/*/figcaption/text()
}

# clean carousel
@remove: //div/*[has-class("carousel-thumbs")]
@remove: //p[@id="expo-modal-label"]
@remove: //div[has-class("rn-carousel-controls")]

# pull video from list
@split_parent: //div[has-class("video-wrapper")]
$video_containers: //ul[div[has-class("video-wrapper")]]
@remove: $video_containers/li
<div>: $video_containers

## only 5 articles
@remove: //div[has-class("slick-track")]/div[position() >= 6]

## change afterpost image preview
#<pic>: //img[has-class("article__image")]
#@set_attr(src, @srcset)
#@set_attr(width, "200px")

##
@remove: //div[has-class("article__inner")]/a[position() >= 1]
@remove: //div[has-class("article__details")]/*/*[has-class("article__category") or has-class("article__author")]

## convert to related
<related>: //div[has-class("after-post")]
@set_attr("href", "https://t.me/iv?url=https://discours.io", @href, "&rhash=0688c9787e4f4e"): //related//a

# remove what we don't need in Instant View

## remove popular
@remove: //div[@id="popular-posts"]

## remove left numbers
@remove: //span[has-class("lit-pager")]
@remove: //div[has-class("hide")]

# footer trash cleaned
@remove: //span[has-class("author")]

@remove: //button[has-class("slick-prev")]
@remove: //button[has-class("slick-next")]

@remove: //div[has-class("comments-wrapper")]
@remove: //new-comment
@remove: //*[@name="comments"]

# leave only tags in footer

$footer: //div[has-class("footer-post")]

@remove: $footer//div[has-class("author-time-counters")]
@remove: $footer//donate-form
@remove: $footer//div[has-class("social-buttons-absolute")]

$tags: //ul[has-class("tags")]
@remove: $tags/li/*[has-class("tag-counter")]
<p>: $tags/li
@combine(" • ")
<div>: $tags
